"
Represents a method generated by an AICodeAssistant, providing services to compile and install it in the system.
"
Class {
	#name : 'AISuggestedMethod',
	#superclass : 'Object',
	#instVars : [
		'class',
		'sourceCode'
	],
	#category : 'AICodeAssistant-Base',
	#package : 'AICodeAssistant',
	#tag : 'Base'
}

{ #category : 'testing' }
AISuggestedMethod >> canBeCompiled [

	(class isNil or: [ class exists ]) ifFalse: [ ^ false ].
	[ AICodeAssistant compile: sourceCode in: class realClass ]
		on: Error
		do: [ ^ false ].
	^ true
]

{ #category : 'services' }
AISuggestedMethod >> compile [

	^ AICodeAssistant compile: sourceCode in: class realClass
]

{ #category : 'services' }
AISuggestedMethod >> install [

	| method |
	method := self compile.
	method ifNotNil: [
		self realClass addSelector: method selector withMethod: method ]
]

{ #category : 'printing' }
AISuggestedMethod >> printOn: aStream [

	class ifNotNil: [
		aStream
			nextPutAll: class name;
			nextPutAll: ' >> #' ].
	aStream nextPutAll: sourceCode
]

{ #category : 'accessing' }
AISuggestedMethod >> realClass [

	^class ifNotNil: [ class realClass ]
]

{ #category : 'inquiries' }
AISuggestedMethod >> selector [

	^ AICodeAssistant parseSelector: sourceCode 
]

{ #category : 'accessing' }
AISuggestedMethod >> sourceCode [

	^ sourceCode
]

{ #category : 'accessing' }
AISuggestedMethod >> sourceCode: aString [

	sourceCode := aString
]

{ #category : 'accessing' }
AISuggestedMethod >> suggestedClass [
	
	^class
]

{ #category : 'accessing' }
AISuggestedMethod >> suggestedClass: anAISuggestedClass [
	
	class := anAISuggestedClass
]
